#version 460
#extension GL_NV_mesh_shader : require
 
layout(local_size_x=32) in;
layout(triangles, max_vertices=256, max_primitives=512) out;

out PerVertexData
{
  vec3 normal;
  vec2 texCoord;
  vec3 lightDir;
} v_out[];   

uniform mat4 m_pvm;
uniform vec4 l_dir;
uniform mat4 m_view;
uniform mat3 m_normal;

uniform float scale;
uniform float displacement;

layout(std430, binding = 1) readonly buffer verticesBuffer
{
  float vertices[];
};

layout(std430, binding = 2) readonly buffer normalsBuffer
{
  float normals[];
};

layout(std430, binding = 3) readonly buffer texcoordsBuffer
{
  float texCoords[];
};

layout(std430, binding = 4) readonly buffer indicesBuffer
{
  uint indices[];
};

layout(std430, binding = 5) readonly buffer primitivesBuffer
{
  uint primitives[];
};

layout(std430, binding = 6) readonly buffer meshletsBuffer
{
  uint meshlets[];
};

vec4 transform (vec4 vertex) {

  vec3 transformed = vertex.xyz * scale;
  transformed.x += displacement;

  return vec4 (transformed, vertex.w);

}

void main()
{

  // Inputs
  uint local_id  = gl_LocalInvocationID.x;
  uint global_id  = gl_GlobalInvocationID.x;
  uint meshlet_id = gl_WorkGroupID.x;
  uint workg_len = gl_WorkGroupSize.x;

  // Meshlet Info
  uint indices_start = meshlets[meshlet_id * 4 + 0];
  uint indices_count = meshlets[meshlet_id * 4 + 1];
  uint primitives_start = meshlets[meshlet_id * 4 + 2];
  uint primitives_count = meshlets[meshlet_id * 4 + 3];

  // No. Primitives
  gl_PrimitiveCountNV = primitives_count / 3;

  // Vertices
  uint vertices_per_thread = (indices_count + workg_len - 1) / workg_len;
  for (int i=0; i < vertices_per_thread; i++) {

    uint idx = min (i * workg_len + local_id, indices_count - 1);
    uint vIndex = indices[(indices_start + idx) * 3 + 0];
    uint nIndex = indices[(indices_start + idx) * 3 + 1];
    uint tIndex = indices[(indices_start + idx) * 3 + 2];

    vec4 vertex = transform(vec4(vertices[vIndex * 4 + 0],
                                 vertices[vIndex * 4 + 1],
                                 vertices[vIndex * 4 + 2],
                                 vertices[vIndex * 4 + 3]));

    vec3 normal = vec3(normals[nIndex * 3 + 0],
                       normals[nIndex * 3 + 1],
                       normals[nIndex * 3 + 2]);

    vec2 texCoord = vec2(texCoords[tIndex * 3 + 0],
                         texCoords[tIndex * 3 + 1]);

    gl_MeshVerticesNV[idx].gl_Position = m_pvm * vertex;
    v_out[idx].normal = normalize(m_normal * normal);
    v_out[idx].texCoord = texCoord;
    v_out[idx].lightDir = normalize(vec3(m_view * l_dir));
  }

  // Primitives
  uint primitives_per_thread = (primitives_count / 3 + workg_len - 1) / workg_len;
  for (int i=0; i < primitives_per_thread; i++) {

    uint idx = min ((i * workg_len + local_id) * 3, primitives_count - 3);

    gl_PrimitiveIndicesNV[idx + 0] = primitives[primitives_start + idx + 0];
    gl_PrimitiveIndicesNV[idx + 1] = primitives[primitives_start + idx + 1];
    gl_PrimitiveIndicesNV[idx + 2] = primitives[primitives_start + idx + 2];

  }
 
}
